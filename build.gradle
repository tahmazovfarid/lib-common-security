plugins {
    id 'checkstyle'
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
}

group = 'az.ailab'
version = properties.projectVersion
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
sourceCompatibility = javaVersion

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    api "org.springframework.boot:spring-boot-starter-security"
    api "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation "io.github.openfeign:feign-annotation-error-decoder:${openFeignVersion}"
    implementation "io.github.openfeign:feign-jackson:${openFeignVersion}"

    api "io.jsonwebtoken:jjwt-api:${jsonWebTokenVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:${jsonWebTokenVersion}"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jsonWebTokenVersion}"

    implementation "com.github.tahmazovfarid:lib-common:${libCommonVersion}"

    api "javax.validation:validation-api:${validationVersion}"
    implementation "org.apache.commons:commons-lang3:${apacheCommonsLangVersion}"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
}

// Enable the jar task
jar {
    enabled = true
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = properties.gradleVersion
}

checkstyle {
    toolVersion = checkStyleVersion
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
    failOnError = false
}

ext {
    set('springCloudVersion', springCloudVersion)
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}